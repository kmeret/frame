apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: 'realm-android'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "io.github.kmeret.frame"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "API_ENDPOINT", '"https://api.github.com/"'
        try {
            buildConfigField "String", "API_CLIENT_ID", GITHUB_CLIENT_ID
            buildConfigField "String", "API_CLIENT_SECRET", GITHUB_CLIENT_SECRET
        }
        catch (ignored) {
            throw new Exception("Define GITHUB_CLIENT_ID and GITHUB_CLIENT_SECRET in global gradle.properties!")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        kmeret {
            try {
                storeFile file("keystore")
                storePassword KEYSTORE_PASSWORD
                keyAlias "key"
                keyPassword KEY_PASSWORD
            }
            catch (ignored) {
                throw new Exception("Define KEYSTORE_PASSWORD and KEY_PASSWORD in global gradle.properties!")
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.kmeret
        }
        release {
            signingConfig signingConfigs.kmeret

            debuggable false
            minifyEnabled false
            shrinkResources false
            useProguard false

            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    //Kotlin

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0-M1"
//    implementation "androidx.core:core-ktx:1.0.2"

    //DI

    implementation "org.koin:koin-android-viewmodel:2.0.1"

    //network

    def retrofit_version = "2.6.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.2"
    implementation 'com.squareup.picasso:picasso:2.71828'
//    def glide_version = "4.9.0"
//    implementation("com.github.bumptech.glide:glide:$glide_version") {
//        exclude group: "com.android.support"
//    }
//    kapt "com.github.bumptech.glide:compiler:$glide_version"

    //navigation

    implementation "ru.terrakok.cicerone:cicerone:5.0.0"

    //lifecycle

    def lifecycle_version = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    //ui

    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "com.google.android.material:material:1.1.0-alpha07"

    //test

    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
}
